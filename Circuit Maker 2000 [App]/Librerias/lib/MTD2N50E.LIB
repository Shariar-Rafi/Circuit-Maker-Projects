********************************************************************************************
*                                                                                          *
*                     #################################################                    *
*                     #                                               #                    *
*                     #           MTD2N50E BY MOTOROLA                #                    *
*                     #                                               #                    *
*                     #           MODEL DEVELOPED BY                  #                    *
*                     #           ALLIANCE TECHNOLOGIES               #                    *
*                     #           10510 RESEARCH RD SE                #                    *
*                     #           ALBUQUERQUE  NM 87123               #                    *
*                     #           (505) 299-9591                      #                    *
*                     #           12-14-92 A. NUNEZ                   #                    *
*                     #                                               #                    *
*                     #################################################                    *
*                                                                                          *
********************************************************************************************
*
* THE MODEL FOR THIS DEVICE IS A SUBCIRCUIT AND CAN BE USED IN THE FOLLOWING FORMAT
* IN ANY CIRCUIT SIMULATION:
* X<name> Nodes<N1, N2, N3> Model_Name
* where X<name> is the circuit specific name
* Nodes are 3 for this device, the first is the drain, the second is the gate, and the third
* is the source.
* Model_Name is   MTD2N50EX  WHERE 'X' IS EITHER C, P, OR G FOR SPICE 3C.1 - 3E.2, PSPICE,
* AND SPICE 2G.6 RESPECTIVELY
* Example: X1 10 20 30 MTD2N50EC IS USED WITH SPICE 3C.1 COMPATIBALE SIMULATORS
*
.SUBCKT MTD2N50EC         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       1.313     RD
RD2     4       5       1.313     RD
RS      3       9       73.40E-03 RS
RG      1       8       7
LD      5       10      4.5E-9
LG      8       20      7.5E-9
LS      9       30      7.5E-9
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       34.00E-03  RDIO
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.33E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W= 1.014  L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7
+ NSUB   = 9E15            VTO    = 4.1986288       PHI    = 0.6                
+ UO     = 81.765          KAPPA  = 1E-3            ETA    = 0                  
+ THETA  = 2E-3            NFS    = 1.67E12                                                )        
.MODEL   DDS1   D        (                          IS     = 4.9E-12            
+ N      = 1.109           BV     = 500             IBV    = 2.4E-4             
+ EG     = 1.11            XTI    = 5.428           TT     = 1.45e-6               
+ CJO    = 2.3108E-10      VJ     = 0.8063462       M      = 0.5780846          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 0.33E-9 
+ VJ     = 0.450           M      = 0.9000          FC     = 0.5                           )
.MODEL   RD     R        ( TC1    = 9.37174E-3      TC2    = 2.84562E-5                    )
.MODEL   RS     R        ( TC1    = -3.31738E-3     TC2    = 5.09245E-5                    )
.MODEL   RDIO   R        ( TC1    = 3.12052E-3      TC2    = 7.86346E-6                    )
.ENDS
********************************************************************************************
******************************************************************************************** 
.SUBCKT MTD2N50EP         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       RD        1.313    
RD2     4       5       RD        1.313    
RS      3       9       RS        73.40E-03 
RG      1       8       7
LD      5       10      4.5E-9
LG      8       20      7.5E-9
LS      9       30      7.5E-9
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       RDIO      34.00E-03
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.33E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W= 1.014  L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7
+ NSUB   = 9E15            VTO    = 4.1986288       PHI    = 0.6                
+ UO     = 81.765          KAPPA  = 1E-3            ETA    = 0                  
+ THETA  = 2E-3            NFS    = 1.67E12                                                )        
.MODEL   DDS1   D        (                          IS     = 4.9E-12            
+ N      = 1.109           BV     = 500             IBV    = 2.4E-4             
+ EG     = 1.11            XTI    = 5.428           TT     = 1.45e-6               
+ CJO    = 2.3108E-10      VJ     = 0.8063462       M      = 0.5780846          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 0.33E-9 
+ VJ     = 0.450           M      = 0.9000          FC     = 0.5                           )
.MODEL   RD     RES      ( TC1    = 9.37174E-3      TC2    = 2.84562E-5                    )
.MODEL   RS     RES      ( TC1    = -3.31738E-3     TC2    = 5.09245E-5                    )
.MODEL   RDIO   RES      ( TC1    = 3.12052E-3      TC2    = 7.86346E-6                    )
.ENDS
********************************************************************************************
********************************************************************************************
.SUBCKT MTD2N50EG         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       1.313     TC=9.37174E-3,2.84562E-5
RD2     4       5       1.313     TC=9.37174E-3,2.84562E-5
RS      3       9       73.40E-03 TC=-3.31738E-3,5.09245E-5
RG      1       8       7
LD      5       10      4.5E-9
LG      8       20      7.5E-9
LS      9       30      7.5E-9
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       34.00E-03 TC=3.12052E-3,7.86346E-6
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.33E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W= 1.014  L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7
+ NSUB   = 9E15            VTO    = 4.1986288       PHI    = 0.6                
+ UO     = 81.765          KAPPA  = 1E-3            ETA    = 0                  
+ THETA  = 2E-3            NFS    = 1.67E12                                                )        
.MODEL   DDS1   D        (                          IS     = 4.9E-12            
+ N      = 1.109           BV     = 500             IBV    = 2.4E-4             
+ EG     = 1.11            XTI    = 5.428           TT     = 1.45e-6               
+ CJO    = 2.3108E-10      VJ     = 0.8063462       M      = 0.5780846          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 0.33E-9 
+ VJ     = 0.450           M      = 0.9000          FC     = 0.5                           )
.ENDS
