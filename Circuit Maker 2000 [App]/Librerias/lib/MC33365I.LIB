* ------------------------------------------------------------ *
*
* MOTOROLA Semiconductor Product Sector
* Semiconductor Component Group
* MC33365 model developed by Christophe BASSO, Toulouse (FRANCE)
* e-mail: R38010@email.sps.mot.com
* INTUSOFT's IsSpice4 compatible
*
* Last modified: 3/13/99
*
* ------------------------------------------------------------ *
**********
*SRC=MC33365;MC33365;MOTOROLA;SMPS Controller;Brown-out protection
*SYM=MC33365
.SUBCKT MC33365 1   3   4 5 6  7  8   9    10 11  12 13 16 {RDSON=15}
*               STR VCC GND RT CT REG COMP VF BOK GND   DRAIN
**** ISUPPLY ****
BSUPP 3 4 I=V(33)<3.5 ? 250U : 3.5M
**** REFERENCE VOLTAGE ****
BREF 80 4 V=V(3)<7.7 ? V(3)-1.2 : 6.5
ROUT 80 8 10
**** START-UP, UVLO AND BROWNOUT PROTECTION ****
XUVLO 3 81 UVLO
BBOK 3 11 I= V(82)>3.5 ? 50uA : 100nA
VBOK 120 4 1.25
XOVL 11 120 82 COMP2
BSTRT 1 3 I=V(81)<3.5 ? (0.2M*((14-V(3))/14)+1.6M) : 40U
XAND 81 82 33 AND2
**** OSCILLATOR SECTION ****
BCHG 4 7 I=(V(INV1)>500M) & (V(33)>3.5) ? I(VIRT)*4 : 0
BDIS 7 4 I=V(INV1)<500M ? I(VIRT)*4 : 0
DUV 7 33 DUV
VSET 21 4 1.25V
VIRT 21 6
VREF1 22 0 2.5V
RI 22 23 40K
RF 23 INV1 10K
BCMP CLK 4 V=V(7)-V(23)>0 ? 5 : 0
BINV 25 4 V=V(CLK)<500M ? 2.5 : 0
RDEL1 25 INV1 100
CEL2 INV1 0 100P
**** PWM AND LATCH ****
XPWM 7 9 31 COMP2
XOR1 33 31 32 34 OR3_IN_INV
XNAND1 34 CLK 35 AND_IN_INV
XFFLOP 35 34 36 37 FFLOP
XNAND2 CLK 36 33 450 3 AND3_IN_INV
RDRV 450 DRV 100
**** ERROR AMPLIFIER ****
VREF2 60 4 2.6V
XERR 60 10 9 4 ERRAMP ; +  - OUT GND
**** CURRENT LIMIT COMPARATOR ****
RLIMIT 40 4 450
B225 4 40 I=I(VIRT)*2.25
XCOMP 50 40 32 COMPDEL
**** MOSFET MODEL ****
CIN DRV 4 200pF
XMOS 16 71 DRV 4 MOS {RON={RDSON} ROFF=1MEG VON=3 VOFF=1}
VISRC 71 4
**** KELVIN AND L.E.B. ****
BIKEL 4 50 I=V(DRV)>V(3)-0.7 ? I(VISRC)/51.2 : 0
RKEL 50 4 9
.MODEL DUV D RS=100M TT=50N CJO=10P N=0.1
.ENDS
*INCLUDE MC33365.LIB
********************
.SUBCKT COMPDEL 1 2 3
*               + - Out
* T = 0.693 x Ct x Rt
B1 4 0 V=V(1) > V(2) ? 1V : 0
RT 4 5 100
CT 5 0 3.36NF
B2 10 0 V=V(5) > 500M ? 5V : 0
RD 10 3 100
CD 3 0 100P
.ENDS COMPDEL
**** UVLO CIRCUIT ****
.SUBCKT UVLO  1   2  
*            VIN OUT
S1 1 3 1 0 SUVLO
RUV 3 0 100K
B1 4 0 V= V(3) > 5V ? 5V : 0
RD 4 2 100
CD 2 0 100P
.MODEL SUVLO SW RON=1 ROFF=1E6 VT=12 VH=2.5
.ENDS UVLO
**** T FFLOP Circuit ****
.SUBCKT FFLOP 6 8 2 1
*             S R Q Q\
BQB   10 0 V=(V(8)<800M) & (V(2)>800M) ? 0 : 5V
BQ    20 0 V=(V(6)<800M) & (V(1)>800M) ? 0 : 5V
RD1   10 1 100
CD1   1 0 10p IC=5
RD2   20 2 100
CD2   2 0 10p IC=0
.ENDS FFLOP
**** 2 INPUT, ONE INPUT IS INV, AND CIRCUIT ****
.SUBCKT AND_IN_INV 1   2    3
*                 INV NINV OUT
B1 4 0 V= (V(1)<3.5V) & (V(2)>800M) ? 5V : 0
RD 4 3 100
CD 3 0 10P
.ENDS AND_IN_INV
**** 3 INPUT, ONE INPUT IS INV. AND CIRCUIT ****
.SUBCKT AND3_IN_INV  1   2    3    4  6
*                   INV NINV NINV OUT VCC 
B1 5 0 V= (V(1)<3.5V) & (V(2)>800M) & (V(3)>800M) ? V(6) : 0
RD 5 4 10
CD 4 0 100P
.ENDS AND3_IN_INV
**** 3 INPUT, ONE INPUT IS INV. OR CIRCUIT ****
.SUBCKT OR3_IN_INV 1   2    3    4
*                 INV NINV NINV OUT
B1 5 0 V= (V(1)<3.5V) | (V(2)>800M) | (V(3)>800M) ? 5V : 0
RD 5 4 100
CD 4 0 10P
.ENDS OR3_IN_INV
**** 2 INPUT AND CIRCUIT ****
.SUBCKT AND2 1 2 3
B1 4 0 V= (V(1)>800M) & (V(2)>800M) ? 5V : 0
RD 4 3 100
CD 3 0 10P
.ENDS AND2
**** 2 INPUT NOR CIRCUIT ****
.SUBCKT NOR2 1 2 3
B1 4 0 V= (V(1)>800M) | (V(2)>800M) ? 0V : 5
RD 4 3 100
CD 3 0 10P
.ENDS NOR2
**** 2 INPUT COMPARATOR ****
.SUBCKT COMP2 1 2 3
*             + - S
B1 4 0 V=V(1) > V(2) ? 5V : 0
RD 4 3 100
CD 3 0 10P
.ENDS COMP2
**** ERROR AMPLIFIER MODEL ****
.SUBCKT ERRAMP 20 8  3  21
*   	       +  - OUT GND
RIN 20 8 8MEG
CP1 11 21 11.5P	; {1/(6.28*(GAIN/100U)*POLE)}
E1 5 21 11 21 1
R9 5 2 5
D14 2 13 DMOD
ISINK 13 21 2.7U	; {ISINK/100} in mA
Q1 21 13 16 QPMOD
ISOURCE 7 3 100u ; {ISOURCE} in uA
D12 3 7 DMOD
D15 21 11 DCLAMP
G1 21 11 20 8 100U
V1 7 21 5.8			; Vhigh - 0.6V
V4 3 16 250M		; Vlow - 38mV
RP1 11 21 125.9MEG	; {GAIN/100U}
.MODEL QPMOD PNP
.MODEL DCLAMP D (RS=10 BV=5 IBV=0.01)
.MODEL DMOD D TT=1N CJO=10P
.ENDS ERRAMP
**** INTERNAL MOSFET MODEL ****
.SUBCKT MOS 1 2 3 4 {RON=12 ROFF=1MEG VON=7 VOFF=1}
*Connections + - NC+ NC-
B1 1 2 I=V(3,4) < {VOFF} ? V(1,2)/{ROFF} : V(3,4) > {VON} ?
+ V(1,2)/{RON} : V(1,2)/ (EXP(LN({(RON*ROFF)^.5}) + 
+ 3 * LN({RON/ROFF}) * (V(3,4) - {(VON+VOFF)/2}) / ({2* (VON-VOFF)}) - 
+ 2 * LN({RON/ROFF}) * (V(3,4) - {(VON+VOFF)/2})^3 / {(VON-VOFF)^3} ))
.ENDS
********
