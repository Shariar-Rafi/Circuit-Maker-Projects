* ------------------------------------------------------------ *
*
* MOTOROLA Semiconductor Product Sector
* Semiconductor Component Group
* MC33363 model developed by Christophe BASSO, Toulouse (FRANCE)
* email: R38010@email.sps.mot.com
* MICROSIM's PSpice compatible
* Requires PSpice 6.2a or higher
*
* Last modified: 3/13/99
*
* ------------------------------------------------------------ *
***********
.SUBCKT MC33363 1   3   4 5 6  7  8   9    10 11  12 13 16 PARAMS: RDSON=14
*               STR VCC GND RT CT REG COMP VF OVL GND   DRAIN
**** ISUPPLY ****
GSUPP 3 4 VALUE = { IF( V(33)<3.5, 250U, 3.5M ) }
**** REFERENCE VOLTAGE ****
EREF 80 4 VALUE = { IF( V(3)<7.7, V(3)-1.2, 6.5 ) }
ROUT 80 8 10
**** STARTUP AND UVLO ****
XUVLO 3 81 UVLO
XOVL 60 11 82 COMP2
GSTRT 1 3 VALUE = { IF( V(81)<3.5, (14M*((14-V(3))/14)+6M), 40U ) }
XAND 81 82 33 AND2
**** OSCILLATOR SECTION ****
GCHG 4 7 VALUE = { IF( ((V(INV1)>500M) & (V(33)>3.5)), I(VIRT)*4, 0 ) }
GDIS 7 4 VALUE = { IF( V(INV1)<500M, I(VIRT)*4, 0 ) }
DUV 7 33 DUV
VSET 21 4 1.25V
VIRT 21 6
VREF1 22 4 2.5V
RI 22 23 40K
RF 23 INV1 10K
ECMP CLK 4 VALUE = { IF( (V(7)-V(23)>0), 5, 0 ) }
EINV 25 4 VALUE = { IF( V(CLK)<500M, 2.5, 0 ) }
RDEL1 25 INV1 100
CEL2 INV1 4 100P
**** PWM AND LATCH ****
XPWM 7 9 31 COMP2
XOR1 33 31 32 34 OR3_IN_INV
XNAND1 34 CLK 35 AND_IN_INV
XFFLOP 35 34 36 37 FFLOP
XNAND2 CLK 36 33 450 3 AND3_IN_INV
RDRV 450 DRV 100
**** ERROR AMPLIFIER ****
VREF2 60 4 2.6V
XERR 60 10 9 4 ERRAMP ; +  - OUT GND
**** CURRENT LIMIT COMPARATOR ****
RLIMIT 40 4 450
G225 4 40 VALUE = { I(VIRT)*2.25 }
XCOMP 50 40 32 COMPDEL
**** MOSFET MODEL ****
CIN DRV 4 200pF
SMOS 16 71 DRV 4 MOS
VISRC 71 4
**** KELVIN AND L.E.B. ****
GIKEL 4 50 VALUE = { IF( V(DRV)>V(3)-0.7, I(VISRC)/51.2, 0 ) }
RKEL 50 4 9
.MODEL DUV D RS=100M TT=50N CJO=10P N=0.1
.ENDS
**********
.MODEL MOS VSWITCH (RON={RDSON} ROFF=1MEG VON=3 VOFF=1)
**** DELAYED COMPARATOR ****
.SUBCKT COMPDEL 1 2 3
*               + - Out
E1 4 0 VALUE = { IF( V(1) > V(2), 1V, 0 ) }
RT 4 5 100
CT 5 0 3.36NF
E2 10 0 VALUE = { IF( V(5) > 500M, 5V, 0 ) }
RD 10 3 100
CD 3 0 100P
.ENDS COMPDEL
**** UVLO CIRCUIT ****
.SUBCKT UVLO  1   2  
*            VIN OUT
S1 1 3 1 0 SUVLO
RUV 3 0 100K
E1 4 0 VALUE = { IF( V(3) > 5V, 5V, 0 ) }
RD 4 2 100
CD 2 0 100P
.MODEL SUVLO VSWITCH (RON=1 ROFF=1MEG VON=14.5 VOFF=9.5)
.ENDS UVLO
**** T FFLOP Circuit ****
.SUBCKT FFLOP 6 8 2 1
*             S R Q Q\
EQB 10 0 VALUE = { IF ( (V(8)<800M) & (V(2)>800M), 0, 5V ) }
EQ  20 0 VALUE = { IF ( (V(6)<800M) & (V(1)>800M), 0, 5V ) }
RD1   10 1 100
CD1   1 0 10P IC=5
RD2   20 2 100
CD2   2 0 10P IC=0
.ENDS FFLOP
**** 2 INPUT, ONE INPUT IS INV, AND CIRCUIT ****
.SUBCKT AND_IN_INV 1   2    3
*                 INV NINV OUT
E1 4 0 VALUE = { IF( ((V(1)<3.5V) & (V(2)>800M)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS AND_IN_INV
**** 3 INPUT, ONE INPUT IS INV. AND CIRCUIT ****
.SUBCKT AND3_IN_INV  1   2    3    4  6
*                   INV NINV NINV OUT VCC 
E1 5 0 VALUE = { IF( ((V(1)<3.5V) & (V(2)>800M) & (V(3)>800M)), V(6), 0 ) }
RD 5 4 10
CD 4 0 100P
.ENDS AND3_IN_INV
**** 3 INPUT, ONE INPUT IS INV. OR CIRCUIT ****
.SUBCKT OR3_IN_INV 1   2    3    4
*                 INV NINV NINV OUT
E1 5 0 VALUE = { IF( ((V(1)<3.5V) | (V(2)>800M) | (V(3)>800M)), 5V, 0 ) }
RD 5 4 100
CD 4 0 10P
.ENDS OR3_IN_INV
**** 2 INPUT AND CIRCUIT ****
.SUBCKT AND2 1 2 3
E1 4 0 VALUE = { IF( ((V(1)>800M) & (V(2)>800M)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS AND2
**** 2 INPUT NOR CIRCUIT ****
.SUBCKT NOR2 1 2 3
E1 4 0 VALUE = { IF( ((V(1)>800M) | (V(2)>800M)), 0V, 5 ) }
RD 4 3 100
CD 3 0 10P
.ENDS NOR2
**** 2 INPUT COMPARATOR ****
.SUBCKT COMP2 1 2 3
*             + - S
E1 4 0 VALUE = { IF( (V(1) > V(2)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS COMP2
**** ERROR AMPLIFIER MODEL ****
.SUBCKT ERRAMP 20 8  3  21
*   	       +  - OUT GND
RIN 20 8 8MEG
CP1 11 21 11.5P	; {1/(6.28*(GAIN/100U)*POLE)}
E1 5 21 11 21 1
R9 5 2 5
D14 2 13 DMOD
ISINK 13 21 2.7U	; {ISINK/100} in mA
Q1 21 13 16 QPMOD
ISOURCE 7 3 100u ; {ISOURCE} in uA
D12 3 7 DMOD
D15 21 11 DCLAMP
G1 21 11 20 8 100U
V1 7 21 5.8			; Vhigh - 0.6V
V4 3 16 250M		; Vlow - 38mV
RP1 11 21 125.9MEG	; {GAIN/100U}
.MODEL QPMOD PNP
.MODEL DCLAMP D (RS=10 BV=5 IBV=0.01)
.MODEL DMOD D TT=1N CJO=10P
.ENDS ERRAMP
**** MODELS USED FOR APPLICATION EXAMPLE ****
.SUBCKT XFMR 1 2 3 4 PARAMS: RATIO=1
RP 1 2 1MEG
E 5 4 VALUE = { V(1,2)*RATIO }
G 1 2 VALUE = { I(VM)*RATIO }
RS 6 3 1U
VM 5 6 
.ENDS XFMR
****
.MODEL MUR110 D BV=133.3 CJO=45P IBV=2U IS=13.5N M=.302
+ N=1.59 RS=25.7M TT=50.4N VJ=.75
****
.MODEL MUR120 D BV=266 CJO=15.7P IBV=5U IS=1.08U M=.310
+ N=2.59 RS=55.5M TT=108N VJ=.75
****
.MODEL MUR160 D BV=800 CJO=15.7P IBV=5U IS=1.08U M=.310
+ N=2.59 RS=55.5M TT=108N VJ=.75
****
.MODEL DN5818 D BV=4.00E+01 CJO=1.84E-10 EG=0.69
+ IBV=5.00E-04 IS=1.65E-05 M=.637 N=1.34 RS=5.07E-02
+ TT=7.20E-11 VJ=1.95 XTI=2
****
.SUBCKT TL431 7 6 11
*             K A FDBK
.MODEL DCLAMP D (IS=13.5N RS=25M N=1.59 
+ CJO=45P VJ=.75 M=.302 TT=50.4N BV=34V IBV=1MA)
V1 1 6 2.495
R1 6 2 15.6
C1 2 6 .5U
R2 2 3 100
C2 3 4 .08U
R3 4 6 10
G2 6 8 3 6 1.73
D1 5 8 DCLAMP
D2 7 8 DCLAMP
V4 5 6 2
G1 6 2 1 11 0.11
.ENDS TL431
****
.SUBCKT MOC8101 1 2 3   5
* OPTO-ISOLATOR A C COL EMITTER
RB 4 0 100Meg
VM 1 6
D1 6 2 LED
H1 7 0 VM .0055
R1 7 8 1K
C1 8 0 3.35nF
G1 3 4 8 0 1
Q1 3 4 5 MPSA06
.MODEL LED D(N=1.7 RS=.7 CJO=23.9P IS=85.3p BV=6 IBV=10U 
+ VJ=0.75 M=0.333 TT=4.32U)
.MODEL MPSA06 NPN (IS=15.2F NF=1 BF=589 VAF=98.6 IKF=90M ISE=3.34P NE=2 
+ BR=4 NR=1 VAR=16 IKR=0.135 RE=0.343 RB=1.37 RC=0.137 XTB=1.5
+ CJE=9.67P VJE=1.1 MJE=0.5 CJC=7.34P VJC=0.3 MJC=0.3 TF=10.29n TR=276N)
.ENDS
*****
