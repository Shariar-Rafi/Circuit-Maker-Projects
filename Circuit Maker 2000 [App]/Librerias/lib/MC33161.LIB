*	|==========================================================
*	|
*	|
*	|
*	|			   MC33161
*	|
*	|			ON Semiconductor
*	|
*	|		 Universal Voltage Monitor
*	|
*	|
*	|
*	|
*	|	This model was developed by
*	|
*	|	AEI (Analytic Engineering)
*	|
*	|	182 Morris Avenue
*	|
*	|	Holtsville, NY 11742
*	|
*	|	Copyright 2001 AEI
*	|
*	|	All Rights Reserved
*	|
*	|
*	|
*	|	The content of this model is subject to change
*	|
*	|	without notice and may not be modified or altered
*	|
*	|	without permission from On Semiconductor. This model
*	|
*	|	has been carefully checked and is believed to be
*	|
*	|	accurate, however neither AEI nor On Semiconductor
*	|
*	|	assume liability for the use of this model or the
*	|
*	|	results obtained from using it.
*	|
*	|
*	|
*	|	For more information regarding modeling services,
*	|
*	|	model libraries or simulation productors, please
*	|
*	|	call AEI at (631) 654-0253 ext 116 or 106.
*	|
*	|	email: Sales@aeng.com.
*	|
*	|
*	|
*	|==========================================================
*SYM=MC33161
.SUBCKT MC33161 23   5   24  7   22   15   1    3
*               VREF IN1 IN2 GND OUT2 OUT1 MODE VCC
.MODEL COMP SW (VT=1.27 VH=0.025 RON=1K ROFF=10MEG)
.MODEL DMOD D (IS=1E-16)
.MODEL QN1 NPN (BF=200 RC=8.5)
.MODEL QN2 NPN (BF=50)
.MODEL M1 NMOS VTO=2.3 KP=100
R1 2 3 1MEG
BCOND 4 7 V=V(2) > 2.5 ? 5 : 0
R3 9 7 250K
V3 11 10 2
R4 10 6 12K
I1 6 7 24U
D2 7 6 DMOD
D5 1 9 DMOD
D6 9 11 DMOD
BCOMP1 12 7 V=2.8 > V(1) ? 5 : 0
BXOR1 13 7 V=ABS(V(12,4)) < 2.5 ? 0 : 5
R5 26 13 44K
Q1 15 26 7 QN1
S2 17 7 24 7 COMP
R8 17 3 1MEG
B2 16 7 V=V(17) > 2.5 ? 5 : 0
BCOM2B 20 7 V=0.6 > V(1) ? 5 : 0
B4 19 7 V=ABS(V(20,16)) < 2.5 ? 0 : 5
R9 18 19 44K
Q3 22 18 7 QN1
Q4 3 21 25 QN2
R12 3 21 25K
R13 25 23 0.3
R15 23 27 10K
R16 27 7 100K
M1 21 27 7 7 M1
S1 2 7 5 7 COMP
.ENDS
