********************************************************************************************
*                                                                                          *
*                     #################################################                    *
*                     #                                               #                    *
*                     #           MTD3N25E BY MOTOROLA                #                    *
*                     #                                               #                    *
*                     #           MODEL DEVELOPED BY                  #                    *
*                     #           ALLIANCE TECHNOLOGIES               #                    *
*                     #           10510 RESEARCH RD SE                #                    *
*                     #           ALBUQUERQUE  NM 87123               #                    *
*                     #           (505) 299-9591                      #                    *
*                     #           12-03-92 A. NUNEZ                   #                    *
*                     #                                               #                    *
*                     #################################################                    *
*                                                                                          *
********************************************************************************************
*
* THE MODEL FOR THIS DEVICE IS A SUBCIRCUIT AND CAN BE USED IN THE FOLLOWING FORMAT
* IN ANY CIRCUIT SIMULATION:
* X<name> Nodes<N1, N2, N3> Model_Name
* where X<name> is the circuit specific name
* Nodes are 3 for this device, the first is the drain, the second is the gate, and the third
* is the source.
* Model_Name is   MTD3N25EX  WHERE 'X' IS EITHER C, P, OR G FOR SPICE 3C.1 - 3E.2, PSPICE,
* AND SPICE 2G.6 RESPECTIVELY
* Example: X1 10 20 30 MTD3N25EC IS USED WITH SPICE 3C.1 COMPATIBALE SIMULATORS
*
.SUBCKT MTD3N25EC         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       458.9E-03 RD
RD2     4       5       458.9E-03 RD
RS      3       9       147.5E-03 RS
RG      1       8       17
LD      5       10      3.5E-29
LG      8       20      7.5E-29
LS      9       30      7.5E-29
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       32.29E-3   RDIO
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.29E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=.81   L=1E-6
CGS     1       3       14E-12
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7
+ NSUB   = 3E15            VTO    = 3.88            PHI    = 0.6                
+ UO     = 193.73          KAPPA  = 0               ETA    = 0                  
+ THETA  = 0.1758          NFS    = 2.87E12                                                )                                          
.MODEL   DDS1   D        (                          IS     = 2.036142E-12       
+ N      = 1.0640402       BV     = 250             IBV    = 2.5E-4             
+ EG     = 1.11            XTI    = 4.915           TT     = 615E-9               
+ CJO    = 2.47E-10        VJ     = 1.1612853       M      = 0.5804441          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 2.67E-10 
+ VJ     = 1.410           M      = 0.900           FC     = 0.5                           )
.MODEL   RD     R        ( TC1    = 9.73122E-3      TC2    = 2.41728E-5                    )
.MODEL   RS     R        ( TC1    = -2.37284E-3     TC2    = 5.84772E-6                    )
.MODEL   RDIO   R        ( TC1    = 2.72851E-3      TC2    = 7.75201E-6                    )
.ENDS
********************************************************************************************
********************************************************************************************
.SUBCKT MTD3N25EP         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       RD        458.9E-03 
RD2     4       5       RD        458.9E-03 
RS      3       9       RS        147.5E-03
RG      1       8       17       
LD      5       10      3.5E-29
LG      8       20      7.5E-29
LS      9       30      7.5E-29
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       RDIO      32.29E-3
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.29E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=.81   L=1E-6
CGS     1       3       14E-12
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7
+ NSUB   = 3E15            VTO    = 3.88            PHI    = 0.6                
+ UO     = 193.73          KAPPA  = 0               ETA    = 0                  
+ THETA  = 0.1758          NFS    = 2.87E12                                                )                                          
.MODEL   DDS1   D        (                          IS     = 2.036142E-12       
+ N      = 1.0640402       BV     = 250             IBV    = 2.5E-4             
+ EG     = 1.11            XTI    = 4.915           TT     = 615E-9               
+ CJO    = 2.47E-10        VJ     = 1.1612853       M      = 0.5804441          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 2.67E-10 
+ VJ     = 1.410           M      = 0.900           FC     = 0.5                           )
.MODEL   RD     RES      ( TC1    = 9.73122E-3      TC2    = 2.41728E-5                    )
.MODEL   RS     RES      ( TC1    = -2.37284E-3     TC2    = 5.84772E-6                    )
.MODEL   RDIO   RES      ( TC1    = 2.72851E-3      TC2    = 7.75201E-6                    )
.ENDS                                                                                       
********************************************************************************************
********************************************************************************************
.SUBCKT MTD3N25EG        10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       458.9E-03   TC=9.73122E-3,2.41728E-5                    
RD2     4       5       458.9E-03   TC=9.73122E-3,2.41728E-5                    
RS      3       9       147.5E-03   TC=-2.37284E-3,5.84772E-6
RG      1       8       17       
LD      5       10      3.5E-29
LG      8       20      7.5E-29
LS      9       30      7.5E-29
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       32.29E-3    TC=2.72851E-3,7.75201E-6
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.29E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=.81   L=1E-6
CGS     1       3       14E-12
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7
+ NSUB   = 3E15            VTO    = 3.88            PHI    = 0.6                
+ UO     = 193.73          KAPPA  = 0               ETA    = 0                  
+ THETA  = 0.1758          NFS    = 2.87E12                                                )                                          
.MODEL   DDS1   D        (                          IS     = 2.036142E-12       
+ N      = 1.0640402       BV     = 250             IBV    = 2.5E-4             
+ EG     = 1.11            XTI    = 4.915           TT     = 615E-9               
+ CJO    = 2.47E-10        VJ     = 1.1612853       M      = 0.5804441          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 2.67E-10 
+ VJ     = 1.410           M      = 0.900           FC     = 0.5                           )
.ENDS


