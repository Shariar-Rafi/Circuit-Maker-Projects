********************************************************************************************
*                                                                                          *
*                     #################################################                    *
*                     #                                               #                    *
*                     #           MTB20N20E  BY MOTOROLA              #                    *
*                     #                                               #                    *
*                     #           MODEL DEVELOPED BY                  #                    *
*                     #           ALLIANCE TECHNOLOGIES               #                    *
*                     #           10510 RESEARCH RD SE                #                    *
*                     #           ALBUQUERQUE  NM 87123               #                    *
*                     #           (505) 299-9591                      #                    *
*                     #           11-25-92 J. DULIERE                 #                    *
*                     #                                               #                    *
*                     #################################################                    *
*                                                                                          *
********************************************************************************************
*
* THE MODEL FOR THIS DEVICE IS A SUBCIRCUIT AND CAN BE USED IN THE FOLLOWING FORMAT
* IN ANY CIRCUIT SIMULATION:
* X<name> Nodes<N1, N2, N3> Model_Name
* where X<name> is the circuit specific name
* Nodes are 3 for this device, the first is the drain, the second is the gate, and the third
* is the source.
* Model_Name is   MTB20N20X  WHERE 'X' IS EITHER C, P, OR G FOR SPICE 3C.1 - 3E.2, PSPICE,
* AND SPICE 2G.6 RESPECTIVELY
* Example: X1 10 20 30 MTB20N20C IS USED WITH SPICE 3C.1 COMPATIBALE SIMULATORS
*                                                                                          
.SUBCKT MTB20N20C         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       54.25E-03 RD
RD2     4       5       54.25E-03 RD
RS      3       9       45.20E-03 RS
RG      1       8       7.25
LD      5       10      3.5E-9
LG      8       20      7.5E-9
LS      9       30      7.5E-9
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       9.1E-03   RDIO
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       1.82E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=5.36   L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 1.85E15         VTO    = 4.472           PHI    = 0.6                
+ UO     = 690.9843806     KAPPA  = 7.131283E-4     THETA  = 6.680866E-3        
+ NFS    = 2.5E12                                                                          )   
.MODEL   DDS1   D        (                          IS     = 3.908008E-12       
+ N      = 1.012433        BV     = 200             IBV    = 2.5E-4             
+ EG     = 1.11            XTI    = 3.497           TT     = 753E-9               
+ CJO    = 1.662516E-9     VJ     = 0.7407019       M      = 0.4736603          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 1.70e-9 
+ VJ     = 0.217           M      = 0.692           FC     = 0.5                           )
.MODEL   RD     R        ( TC1    = 7.6984E-4       TC2    = 5.43994E-6                    )
.MODEL   RS     R        ( TC1    = 9.64295E-3      TC2    = 2.3704E-5                     )
.MODEL   RDIO   R        ( TC1    = 2.06379E-3      TC2    = 6.53716E-7                    )
.ENDS
********************************************************************************************
********************************************************************************************
.SUBCKT MTB20N20P         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       RD        54.25E-03
RD2     4       5       RD        54.25E-03
RS      3       9       RS        45.20E-03
RG      1       8       7.25
LD      5       10      3.5E-9
LG      8       20      7.5E-9
LS      9       30      7.5E-9
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       RDIO      9.1E-03   
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       1.82E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=5.36   L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 1.85E15         VTO    = 4.472           PHI    = 0.6                
+ UO     = 690.9843806     KAPPA  = 7.131283E-4     THETA  = 6.680866E-3        
+ NFS    = 2.5E12                                                                          )   
.MODEL   DDS1   D        (                          IS     = 3.908008E-12       
+ N      = 1.012433        BV     = 200             IBV    = 2.5E-4             
+ EG     = 1.11            XTI    = 3.497           TT     = 753E-9               
+ CJO    = 1.662516E-9     VJ     = 0.7407019       M      = 0.4736603          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 1.70e-9 
+ VJ     = 0.217           M      = 0.692           FC     = 0.5                           )
.MODEL   RD     RES      ( TC1    = 7.6984E-4       TC2    = 5.43994E-6                    )
.MODEL   RS     RES      ( TC1    = 9.64295E-3      TC2    = 2.3704E-5                     )
.MODEL   RDIO   RES      ( TC1    = 2.06379E-3      TC2    = 6.53716E-7                    )
.ENDS                             
********************************************************************************************
********************************************************************************************
.SUBCKT MTB20N20G         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       54.25E-03   TC=7.6984E-4,5.43994E-6 
RD2     4       5       54.25E-03   TC=7.6984E-4,5.43994E-6 
RS      3       9       45.20E-03   TC=9.64295E-3,2.3704E-5
RG      1       8       7.25
LD      5       10      3.5E-9
LG      8       20      7.5E-9
LS      9       30      7.5E-9
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       9.1E-03     TC=2.06379E-3,6.53716E-7
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       1.82E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=5.36   L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 1.85E15         VTO    = 4.472           PHI    = 0.6                
+ UO     = 690.9843806     KAPPA  = 7.131283E-4     THETA  = 6.680866E-3        
+ NFS    = 2.5E12                                                                          )   
.MODEL   DDS1   D        (                          IS     = 3.908008E-12       
+ N      = 1.012433        BV     = 200             IBV    = 2.5E-4             
+ EG     = 1.11            XTI    = 3.497           TT     = 753E-9               
+ CJO    = 1.662516E-9     VJ     = 0.7407019       M      = 0.4736603          
+ FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 1.70e-9 
+ VJ     = 0.217           M      = 0.692           FC     = 0.5                           )
.ENDS     


_
