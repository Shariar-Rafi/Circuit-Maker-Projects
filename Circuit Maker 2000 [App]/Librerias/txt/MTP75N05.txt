********************************************************************************************
*                                                                                          *
*                     #################################################                    *
*                     #                                               #                    *
*             		      #           MTP75N05HD BY MOTOROLA              #                    *
*                     #                                               #                    *
*                     #           MODEL DEVELOPED BY                  #                    *
*                     #           ALLIANCE TECHNOLOGIES               #                    *
*                     #           10510 RESEARCH RD SE                #                    *
*                     #           ALBUQUERQUE  NM 87123               #                    *
*                     #           (505) 299-9591                      #                    *
*                     #           11-11-92 J. DULIERE                 #                    *
*                     #                                               #                    *
*                     #################################################                    *
*                                                                                          *
********************************************************************************************
*
* THE MODEL FOR THIS DEVICE IS A SUBCIRCUIT AND CAN BE USED IN THE FOLLOWING FORMAT
* IN ANY CIRCUIT SIMULATION:
* X<name> Nodes<N1, N2, N3> Model_Name
* where X<name> is the circuit specific name
* Nodes are 3 for this device, the first is the drain, the second is the gate, and the third
* is the source.
* Model_Name is   MTP75N05X  WHERE 'X' IS EITHER C, P, OR G FOR SPICE 3C.1 - 3E.2, PSPICE,
* AND SPICE 2G.6 RESPECTIVELY
* Example: X1 10 20 30 MTP75N05C IS USED WITH SPICE 3C.1 COMPATIBALE SIMULATORS
*
.SUBCKT MTP75N05C         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       0.0015    RD
RD2     4       5       0.0015    RD
RS      3       9       0.0030    RS
RG      1       8       4
LD      5       10      4.5E-09
LG      8       20      7.5E-09
LS      9       30      7.5E-09
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       0.0026    RDIO
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E05
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       2.5E-09
RCGD    4       7       1E05
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=6.69   L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 5.5E15          VTO    = 3.84            PHI    = 0.6                
+ UO     = 335.02          KAPPA  = 2.527498E-3     ETA    = 3E-3               
+ THETA  = 0.01            NFS    = 1E12                                                   )                           
.MODEL   DDS1   D        (                          IS     = 7.509945E-12       
+ N      = 1.0332478       BV     = 99.276          IBV    = 0.01               
+ EG     = 1.11            XTI    = 4.2125          TT     = 92E-9               
+ CJO    = 4.102016E-9     VJ     = 1.0014842       M      = 0.5098408          
+ FC     = 0.5                                                                             )                                                    
.MODEL   DGD    D        (                          CJO    = 2.43E-9 
+ VJ     = 1.814           M      = 0.884           FC     = 0.5                           )
.MODEL   RD     R        ( TC1    = 8.73251E-3      TC2    = 7.34669E-6                    )
.MODEL   RS     R        ( TC1    = -5.5296E-3      TC2    = 6.92632E-3                    )
.MODEL   RDIO   R        ( TC1    = 3.16840E-3      TC2    = 3.17472E-6                    )
.ENDS                   
********************************************************************************************
********************************************************************************************
.SUBCKT MTP75N05P        10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       RD        0.0015    
RD2     4       5       RD        0.0015    
RS      3       9       RS        0.0030    
RG      1       8       4
LD      5       10      4.5E-09
LG      8       20      7.5E-09
LS      9       30      7.5E-09
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       RDIO      0.0026    
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E05
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       2.5E-09
RCGD    4       7       1E05
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=6.69   L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 5.5E15          VTO    = 3.84            PHI    = 0.6                
+ UO     = 335.02          KAPPA  = 2.527498E-3     ETA    = 3E-3               
+ THETA  = 0.01            NFS    = 1E12                                                   )                           
.MODEL   DDS1   D        (                          IS     = 7.509945E-12       
+ N      = 1.0332478       BV     = 99.276          IBV    = 0.01               
+ EG     = 1.11            XTI    = 4.2125          TT     = 92E-9               
+ CJO    = 4.102016E-9     VJ     = 1.0014842       M      = 0.5098408          
+ FC     = 0.5                                                                             )                                                    
.MODEL   DGD    D        (                          CJO    = 2.43E-9 
+ VJ     = 1.814           M      = 0.884           FC     = 0.5                           )
.MODEL   RD     RES      ( TC1    = 8.73251E-3      TC2    = 7.34669E-6                    )
.MODEL   RS     RES      ( TC1    = -5.5296E-3      TC2    = 6.92632E-3                    )
.MODEL   RDIO   RES      ( TC1    = 3.16840E-3      TC2    = 3.17472E-6                    )
.ENDS                   
********************************************************************************************
********************************************************************************************
.SUBCKT MTP75N05G        10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       0.0015    TC= 8.73251E-3,7.34669E-6
RD2     4       5       0.0015    TC= 8.73251E-3,7.34669E-6
RS      3       9       0.0030    TC= -5.5296E-3,6.92632E-3
RG      1       8       4
LD      5       10      4.5E-09
LG      8       20      7.5E-09
LS      9       30      7.5E-09
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       0.0026    TC=3.16840E-3,3.17472E-6
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E05
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       2.5E-09
RCGD    4       7       1E05
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=6.69   L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 5.5E15          VTO    = 3.84            PHI    = 0.6                
+ UO     = 335.02          KAPPA  = 2.527498E-3     ETA    = 3E-3               
+ THETA  = 0.01            NFS    = 1E12                                                   )                           
.MODEL   DDS1   D        (                          IS     = 7.509945E-12       
+ N      = 1.0332478       BV     = 99.276          IBV    = 0.01               
+ EG     = 1.11            XTI    = 4.2125          TT     = 92E-9               
+ CJO    = 4.102016E-9     VJ     = 1.0014842       M      = 0.5098408          
+ FC     = 0.5                                                                             )                                                    
.MODEL   DGD    D        (                          CJO    = 2.43E-9 
+ VJ     = 1.814           M      = 0.884           FC     = 0.5                           )
.ENDS                   






