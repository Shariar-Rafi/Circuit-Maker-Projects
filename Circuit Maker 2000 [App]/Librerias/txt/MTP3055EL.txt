********************************************************************************************
*                                                                                          *
*                     #################################################                    *
*                     #                                               #                    *
*                     #           MTP3055EL BY MOTOROLA               #                    *
*                     #                                               #                    *
*                     #           MODEL DEVELOPED BY                  #                    *
*                     #           ALLIANCE TECHNOLOGIES               #                    *
*                     #           10510 RESEARCH RD SE                #                    *
*                     #           ALBUQUERQUE  NM 87123               #                    *
*                     #           (505) 299-9591                      #                    *
*                     #           07-28-93 A. NUNEZ                   #                    *
*                     #                                               #                    *
*                     #################################################                    *
*                                                                                          *
********************************************************************************************
*
* THE MODEL FOR THIS DEVICE IS A SUBCIRCUIT AND CAN BE USED IN THE FOLLOWING FORMAT
* IN ANY CIRCUIT SIMULATION:
* X<name> Nodes<N1, N2, N3> Model_Name
* where X<name> is the circuit specific name
* Nodes are 3 for this device, the first is the drain, the second is the gate, and the third
* is the source.
* Model_Name is   MTP3055EX  WHERE 'X' IS EITHER C, P, OR G FOR SPICE 3C.1 - 3E.2, PSPICE,
* AND SPICE 2G.6 RESPECTIVELY
* Example: X1 10 20 30 MTP3055EC IS USED WITH SPICE 3C.1 COMPATIBALE SIMULATORS
*
.SUBCKT MTP3055EC         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       17.5E-03 RD
RD2     4       5       17.5E-03 RD
RS      3       9       34.2E-03 RS
RG      1       8       4
LD      5       10      3.5E-09
LG      8       20      7.5E-09
LS      9       30      7.5E-09
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       0.0251     RDIO
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.60E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=1.22  L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 4.5612E14       VTO    = 1.803           PHI    = 0.6                
+ UO     = 158.17          KAPPA  = 0               NFS    = 4.256081E11                   )  
.MODEL   DDS1   D        (                          IS     = 5.400306E-11       
+ N      = 1.1855231       BV     = 60.00           IBV    = 0.001          
+ EG     = 1.1435          XTI    = 2.529           TT     = 50E-9            
+ CJO    = 6.090755E-10    VJ     = 0.4851704       M      = 0.3716474       
* FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 0.54E-09 
+ VJ     = 0.350           M      = 0.650           FC     = 0.5                           )
.MODEL   RD     R        ( TC1    = 13.8374E-3      TC2    = 6.38887E-5                    )
.MODEL   RS     R        ( TC1    = -4.71836E-3     TC2    = -1.39768E-5                   )
.MODEL   RDIO   R        ( TC1    = 1.560642E-3     TC2    = 4.60643E-6                    )
.ENDS
********************************************************************************************
********************************************************************************************
.SUBCKT MTP3055EP         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       RD  17.5E-03 
RD2     4       5       RD  17.5E-03 
RS      3       9       RS  34.2E-03 
RG      1       8       17
LD      5       10      3.5E-09
LG      8       20      7.5E-09
LS      9       30      7.5E-09
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       RDIO  0.0251     
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.60E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=1.22  L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 4.5612E14       VTO    = 1.803           PHI    = 0.6                
+ UO     = 158.17          KAPPA  = 0               NFS    = 4.256081E11                   )  
.MODEL   DDS1   D        (                          IS     = 5.400306E-11       
+ N      = 1.1855231       BV     = 60.00           IBV    = 0.001          
+ EG     = 1.1435          XTI    = 2.529           TT     = 1E-9            
+ CJO    = 6.090755E-10    VJ     = 0.4851704       M      = 0.3716474       
* FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 0.54E-09 
+ VJ     = 0.350           M      = 0.650           FC     = 0.5                           )
.MODEL   RD     RES      ( TC1    = 13.8374E-3      TC2    = 6.38887E-5                    )
.MODEL   RS     RES      ( TC1    = -4.71836E-3     TC2    = -1.39768E-5                   )
.MODEL   RDIO   RES      ( TC1    = 1.560642E-3     TC2    = 4.60643E-6                    )
.ENDS
********************************************************************************************
********************************************************************************************
.SUBCKT MTP3055EG         10 20 30
*                         |  |  |
*                         |  |  SOURCE
*                         |  GATE
*                         DRAIN
********************************************************************************************
*                                                                                          *
*------------------------------ EXTERNAL PARASITICS OF DEVICE -----------------------------*
RD1     2       4       17.5E-03   TC=13.8374E-3 ,6.38887E-5                               
RD2     4       5       17.5E-03   TC=13.8374E-3 ,6.38887E-5                               
RS      3       9       34.2E-03   TC=-4.71836E-3,-1.39768E-5                              
RG      1       8       17
LD      5       10      3.5E-09
LG      8       20      7.5E-09
LS      9       30      7.5E-09
********************************************************************************************                                                                                          
*---------------------------- END EXTERNAL PARASITICS OF DEVICE ---------------------------*
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------- DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ---------------------*
DDS     9       11      DDS1     
RDIO    11      5       0.0251     TC=1.560642E-3,4.60643E-6                               )
*-------------------- END DRAIN SOURCE CAPACITANCE AND BREAKDOWN CIRCUIT ------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*---------------------------- DRAIN GATE CAPACITANCE SIMULATOR ----------------------------*
*-------------------- FIRST SECTION FOR NON-LINEAR CGD INCLUDES FOLLOWING -----------------*
********************************************************************************************
*     VOLTAGE CONTROLLED SWITCHES CAN BE USED IN PLACE OF M2 AND M3 IN THE EVENT           *
*     THE SIMULATOR SUPPORTS VOLTAGE CONTROLLED SWITCHES.  USE OF VOLTAGE CONTROLLED       *
*     SWITCHES WILL REDUCE EXECUTION TIME FOR THIS MODEL                                   *
********************************************************************************************
*
DGD     6       4       DGD
RDGD    4       6       1E10
M2      6       4       1       100     MSW1                    
RSUB1   1       100     1E12
*----------------------------- LAST SECTION FOR CONSTANT CGD  -----------------------------*
CGDMAX  4       7       0.60E-9
RCGD    4       7       1E10
M3      7       4       1       101     MSW2
RSUB2   1       101     1E12        
*---------------------------- END GATE CAPACITANCE SIMULATOR ------------------------------*
*                                                                                          *
********************************************************************************************
********************************************************************************************
*                                                                                          *
*--------------------------- LEVEL 3 MOS MODEL CORE OF SUBCKT -----------------------------*
M1      2       1       3       9       MAIN    W=1.22  L=1E-6
*------------------------------ END LEVEL THREE MOS MODEL ---------------------------------*
*                                                                                          *
********************************************************************************************
*------------------------------ PARAMETER SETS FOR MODELS ---------------------------------*
.MODEL   MSW1    NMOS    (LEVEL = 1         VTO  = 0       KP    = 1                       )
.MODEL   MSW2    PMOS    (LEVEL = 1         VTO  = 0       KP    = 1    	           )
.MODEL   MAIN    NMOS    (LEVEL = 3                 TOX    = 1E-7               
+ NSUB   = 4.5612E14       VTO    = 1.803           PHI    = 0.6                
+ UO     = 158.17          KAPPA  = 0               NFS    = 4.256081E11                   )  
.MODEL   DDS1   D        (                          IS     = 5.400306E-11       
+ N      = 1.1855231       BV     = 60.00           IBV    = 0.001          
+ EG     = 1.1435          XTI    = 2.529           TT     = 1E-9            
+ CJO    = 6.090755E-10    VJ     = 0.4851704       M      = 0.3716474       
* FC     = 0.5                                                                             )
.MODEL   DGD    D        (                          CJO    = 0.54E-09 
+ VJ     = 0.350           M      = 0.650           FC     = 0.5                           )
.ENDS




